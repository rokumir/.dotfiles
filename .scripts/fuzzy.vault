#!/usr/bin/env fish

argparse l/label= H/hidden=? print=? absolute=? -- $argv
or return 1

# fuzzy Label
set label ' Home '
if set -q _flag_label
    set label $_flag_label
end

# find options
set searchd_cmd searchd --color never -a -d1 -td
if set -q _flag_hidden
    set -a searchd_cmd -H
end

begin
    printf '%s\n' $RH_VAULT
    $searchd_cmd -- . $RH_VAULT
end \
    | sed "s:^$HOME/::; s:/\$::" \
    | sort -ur \
    | fuzzyf --border-label $label \
    | read -l result
or return 1

set result (set -q _flag_absolute && echo $HOME || echo \~)/$result

if set -q _flag_print
    echo $result
else
    source (type --path helper.open-path) $result
end
