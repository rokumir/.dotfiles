#!/usr/bin/env fish

argparse l/label= H/hidden=? print=? absolute=? -- $argv
or return 1

# FZF Label
set label ' Home '
if set -q _flag_label
    set label $_flag_label
end

# FD options
set fd_opts --color never -a -d1 -td
if set -q _flag_hidden
    set -a fd_opts -H
end

begin
    printf '%s\n' $FZF_HOME_DIRS
    $find_cmd $fd_opts -- . $FZF_HOME_DIRS
end \
    | sed "s:^$HOME/::; s:/\$::" \
    | sort -ur \
    | $fzf_cmd --border-label $label \
    | read -l result
or return 1

set result ~/$result

# Return if the current path is chosen
if test (realpath (eval echo -- $result)) = (pwd) &>/dev/null
    return 1
end

if set -q _flag_print
    echo $result
else
    source (type --path fzf.open-path) $result
end
