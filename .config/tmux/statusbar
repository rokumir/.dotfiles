#!/usr/bin/env fish
# vim:ft=fish

## -----------------------------------------------------------------------------
## Configuration Variables
## -----------------------------------------------------------------------------

# Theme Configuration
set constants \
    LEFT_CONTENT Rokumir \
    RIGHT_CONTENT '%a %d %b %H:%M' \
    SEP_LEFT_FILL  \
    SEP_RIGHT_FILL  \
    SEP_LEFT_TAIL_FILL  \
    SEP_LEFT  \
    SEP_RIGHT 

# Color Configuration
set palette \
    BG default \
    FG '#e0e0e0' \
    FG1 '#949aa8' \
    FG_MUTED '#6e6a86' \
    VIOLET '#a77bca' \
    TEAL '#4db6ac' \
    BLACK '#191724' \
    GRAY '#1f1d2e' \
    GRAY1 '#26233a' \
    GRAY2 '#403d52'

## -----------------------------------------------------------------------------
## Helper Functions
## -----------------------------------------------------------------------------

function t-set -d "Set tmux option"
    if test "$argv[1]" = -w
        tmux set-window-option -gq $argv[2] "$argv[3..]"
    else
        tmux set-option -gq $argv[1] "$argv[2..]"
    end
end

function _const -d "Get theme configuration value"
    set -l index (contains -i -- $argv[1] $constants)
    if test $index
        echo $constants[(math $index + 1)]
    end
end

function _color -d "Get color from palette"
    set -l index (contains -i -- $argv[1] $palette)
    if test $index
        echo $palette[(math $index + 1)]
    end
end

## -----------------------------------------------------------------------------
## Status Bar Components
## -----------------------------------------------------------------------------

function build_left_status -d "Build left status bar content"
    string join '' \
        # prefix indicator
        "#[bg=$(_color BLACK) bold] #{?client_prefix," \
        '' "#[fg=$(_color VIOLET)]$(_const LEFT_CONTENT)," \
        '' "#[fg=$(_color TEAL)]$(_const LEFT_CONTENT)}" \
        # separators
        "#[fg=$(_color BLACK) bg=$(_color GRAY) none]$(_const SEP_LEFT_FILL)" \
        "#[fg=$(_color GRAY) bg=$(_color GRAY1)]$(_const SEP_LEFT_FILL)" \
        "#[fg=$(_color GRAY1) bg=$(_color BG)]$(_const SEP_LEFT_FILL)"
end

function build_right_status -d "Build right status bar content"
    string join '' \
        # time_display
        "#[fg=$(_color FG1) italics] $(_const RIGHT_CONTENT) #[none]" \
        # separators
        "#[fg=$(_color GRAY1) bg=$(_color BG)]$(_const SEP_RIGHT_FILL)" \
        "#[fg=$(_color GRAY) bg=$(_color GRAY1)]$(_const SEP_RIGHT_FILL)" \
        "#[fg=$(_color BLACK) bg=$(_color GRAY)]$(_const SEP_RIGHT_FILL)" \
        # session name
        "#[fg=$(_color TEAL) bg=$(_color BLACK) bold] #S "
end

## -----------------------------------------------------------------------------
## Window Status Formatting
## -----------------------------------------------------------------------------

function configure_window_status -d "Configure window status display"
    set pane_cmd "#[fg=$(_color FG_MUTED)]#{?#{==:#{pane_current_command},fish},, 󰅁#{pane_current_command}󰅂}"
    set pane_title '#(fish -c \'prompt_pwd --full-length-dirs=2 --dir-length=1 | read -l p; test (string length "$p") -gt 16 && echo "…"(string sub -s -16 "$p") || echo "$p"\')'

    #NOTE: ACTIVE
    t-set -w window-status-current-format (build_active_window_format " $pane_title$pane_cmd ")
    #NOTE: IDLE
    t-set -w window-status-format "  #[default fg=$(_color FG1)]$pane_title$pane_cmd #[fg=$(_color GRAY2)]$(_const SEP_LEFT)"
end

function build_active_window_format -a pane_title -d "Build active window status format"
    string join '' \
        "#[fg=$(_color BLACK)]$(_const SEP_LEFT_TAIL_FILL)" \
        "#[fg=$(_color GRAY1) bg=$(_color BLACK)]" \
        "#[fg=$(_color TEAL) noitalics nobold]$pane_title" \
        "#[fg=$(_color BLACK) bg=$(_color BG) nobold]$(_const SEP_LEFT_FILL)"
end

## -----------------------------------------------------------------------------
## Main Configuration
## -----------------------------------------------------------------------------

function apply_base_configuration -d "Apply basic tmux configuration"
    # Status bar basics
    t-set status on
    t-set status-style "bg=$(_color BG) fg=$(_color FG)"
    t-set status-justify center
    t-set status-left-length 100
    t-set status-right-length 100

    # Apply status bar content
    t-set status-left (build_left_status)
    t-set status-right (build_right_status)
end

function apply_mode_styles -d "Apply mode and message styles"
    t-set -w mode-style "fg=$(_color TEAL) bg=$(_color GRAY)"
    t-set message-style "fg=$(_color TEAL) bg=$(_color BG) align=centre"
    t-set message-command-style "fg=$(_color TEAL) bg=$(_color BG) align=centre"
    t-set -w clock-mode-colour (_color TEAL)
end

function apply_window_styles -d "Apply window styling"
    t-set -w window-status-activity-style "fg=$(_color FG) bg=$(_color BG) none"
    t-set -w window-status-separator ""
    t-set -w window-status-style "fg=$(_color FG) bg=$(_color BG) none"
end

## -----------------------------------------------------------------------------
## Initialize Configuration
## -----------------------------------------------------------------------------

function main
    apply_base_configuration
    apply_mode_styles
    apply_window_styles
    configure_window_status
end

# Run the configuration
main
